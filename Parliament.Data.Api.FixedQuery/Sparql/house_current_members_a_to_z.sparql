PREFIX : <http://id.ukpds.org/schema/>
CONSTRUCT {
    _:x :value ?firstLetter .
    ?house
    	a :House ;
    	:houseName ?houseName .
}
WHERE {
	{
	    SELECT DISTINCT ?firstLetter
	    WHERE {
	        BIND(@houseid AS ?house)
	        ?house
	            a :House ;
	            :houseName ?houseName .
	        ?person
	            a :Member;
	            <http://example.com/A5EE13ABE03C4D3A8F1A274F57097B6C> ?listAs .
	        ?incumbency :incumbencyHasMember ?person .
	        FILTER NOT EXISTS { ?incumbency a :PastIncumbency . }
	        {
	            ?incumbency :houseIncumbencyHasHouse ?house .
	    	}
	        UNION {
	            ?incumbency :seatIncumbencyHasHouseSeat ?seat .
	            ?seat :houseSeatHasHouse ?house .
	    	}
	        BIND(ucase(SUBSTR(?listAs, 1, 1)) as ?firstLetter)
	    }
    }
    UNION
    {
    	BIND(@houseid AS ?house)
        ?house
            a :House ;
            :houseName ?houseName .
    }
}
