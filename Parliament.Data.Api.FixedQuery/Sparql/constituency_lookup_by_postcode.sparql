PREFIX parl: <http://id.ukpds.org/schema/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
construct {
    ?constituencyGroup
        a parl:ConstituencyGroup ;
        parl:constituencyGroupName ?constituencyGroupName ;
        parl:constituencyGroupHasHouseSeat ?houseSeat .
    ?houseSeat
        a parl:HouseSeat ;
        parl:houseSeatHasConstituencyGroup ?constituencyGroup .
    ?seatIncumbency
        a parl:SeatIncumbency ;
        parl:incumbencyStartDate ?incStartDate ;
        parl:incumbencyEndDate ?incEndDate ;
        parl:seatIncumbencyHasHouseSeat ?houseSeat .
    ?member
        a parl:Person ;
        parl:personGivenName ?givenName ;
        parl:personFamilyName ?familyName ;
        <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?displayAs ;
        parl:memberHasIncumbency ?seatIncumbency ;
        parl:partyMemberHasPartyMembership ?partyMembership .
    ?partyMembership
        a parl:PartyMembership ;
        parl:partyMembershipHasParty ?party ;
        parl:partyMembershipStartDate ?pmStartDate ;
        parl:partyMembershipEndDate ?pmEndDate .
    ?party
        a parl:Party ;
        parl:partyName ?partyName .
}
where {
    ?constituencyArea a parl:ConstituencyArea;
        parl:constituencyAreaExtent ?constituencyAreaExtent;
        parl:constituencyAreaHasConstituencyGroup ?constituencyGroup.
    ?constituencyGroup parl:constituencyGroupName ?constituencyGroupName.
    bind(strdt(concat("Point(",@longitude," ",@latitude,")"),geo:wktLiteral) as ?point)
    filter(geof:sfWithin(?point,?constituencyAreaExtent))

    optional {
        ?constituencyGroup parl:constituencyGroupHasHouseSeat ?houseSeat .
        ?houseSeat parl:houseSeatHasSeatIncumbency ?seatIncumbency .
        filter not exists { ?seatIncumbency a parl:PastIncumbency . }
        ?seatIncumbency parl:incumbencyStartDate ?incStartDate .
        optional { ?seatIncumbency parl:incumbencyEndDate ?incEndDate . }
        optional { ?seatIncumbency parl:incumbencyHasMember ?member . }
        optional { ?member parl:personGivenName ?givenName . }
        optional { ?member parl:personFamilyName ?familyName . }
        optional { ?member <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?displayAs } .

        optional {
            ?member parl:partyMemberHasPartyMembership ?partyMembership .
            filter not exists { ?partyMembership a parl:PastPartyMembership . }
            ?partyMembership parl:partyMembershipHasParty ?party ;
                             parl:partyMembershipStartDate ?pmStartDate .
            optional { ?partyMembership parl:partyMembershipEndDate ?pmEndDate . }
            ?party parl:partyName ?partyName .
        }
   }
}
