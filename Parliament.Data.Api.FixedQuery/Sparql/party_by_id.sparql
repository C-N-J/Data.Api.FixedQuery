PREFIX : @schemaUri
CONSTRUCT {
    ?party
        a :Party ;
        :partyName ?name ;
        :commonsCount ?commonsCount ;
        :lordsCount ?lordsCount .
}
WHERE {
    SELECT ?party ?name (COUNT(?mp) AS ?commonsCount) (COUNT(?lord) AS ?lordsCount)
    WHERE {
        BIND(@party_id AS ?party)
       	?party a :Party ;
	           :partyName ?name .
        OPTIONAL {
            ?party :partyHasPartyMembership ?partyMembership .
    	  	FILTER NOT EXISTS { ?partyMembership a :PastPartyMembership . }
    	  	?partyMembership :partyMembershipHasPartyMember ?member .
    	  	?member :memberHasParliamentaryIncumbency ?incumbency .
    	  	FILTER NOT EXISTS { ?incumbency a :PastParliamentaryIncumbency . }
            OPTIONAL {
		        ?incumbency a :SeatIncumbency ;
                			:parliamentaryIncumbencyHasMember ?mp .
            }
            OPTIONAL {
                ?incumbency a :HouseIncumbency ;
                            :parliamentaryIncumbencyHasMember ?lord .
            }
        }
    }
    GROUP BY ?party ?name
}
