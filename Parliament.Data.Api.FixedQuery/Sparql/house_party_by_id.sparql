PREFIX : @schemaUri
CONSTRUCT {
    ?house
        a :House ;
        :houseName ?houseName .
    ?party
        a :Party ;
        :partyName ?partyName .
    ?party :count ?currentMemberCount .
}
WHERE {
    {
        SELECT *
        WHERE {
            BIND(@houseid AS ?house)
            ?house :houseName ?houseName .
            OPTIONAL {
                BIND(@partyid AS ?party)
                ?person
                	a :Member ;
                	:partyMemberHasPartyMembership ?partyMembership .
                ?partyMembership :partyMembershipHasParty ?party .
                ?party :partyName ?partyName .
                ?incumbency :incumbencyHasMember ?person .
                {
                	?incumbency :houseIncumbencyHasHouse ?house .
        		}
                UNION {
                	?incumbency :seatIncumbencyHasHouseSeat ?seat .
                	?seat :houseSeatHasHouse ?house .
        		}
        	}
        }
    }
    UNION {
    	SELECT ?party (COUNT(?currentMember) AS ?currentMemberCount)
        WHERE {
        	BIND(@houseid AS ?house)
            BIND(@partyid AS ?party)
            ?house a :House .
            ?party a :Party .
            OPTIONAL {
            	?party :partyHasPartyMembership ?partyMembership .
                FILTER NOT EXISTS { ?partyMembership a :PastPartyMembership . }
                ?partyMembership :partyMembershipHasPartyMember ?currentMember .
                ?currentMember :memberHasIncumbency ?incumbency .
                FILTER NOT EXISTS { ?incumbency a :PastIncumbency . }
                {
                	?incumbency :houseIncumbencyHasHouse ?house .
        		}
                UNION {
                	?incumbency :seatIncumbencyHasHouseSeat ?seat .
                	?seat :houseSeatHasHouse ?house .
        		}
        	}
        }
        GROUP BY ?party
    }
}
